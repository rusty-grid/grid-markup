document = { SOI ~ element+ ~ EOI }
element  = { element_kind ~ attribute_map? ~ content }

element_kind = ${ "~" ~ ident }
ident        = ${ ASCII_ALPHANUMERIC ~ (ASCII_ALPHANUMERIC | "-")* }

attribute_map =  {
    "(" ~ ")"
  | "(" ~ attribute ~ ("," ~ attribute)* ~ ")"
}
attribute     = @{ ident ~ WHITESPACE* ~ ":" ~ WHITESPACE* ~ ident }
WHITESPACE = _{ " " | "\r" | "\n" | "\t" }

string = ${ "\"" ~ inner ~ "\"" }
inner  = @{ char* }
char   =  {
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

content       = ${
    "{" ~ "}"
  | "{" ~ (element | content_inner)+ ~ "}"
}
content_inner = @{ content_char+ }
content_char  =  {
    !("{" | "}" | "~" | "\\") ~ ANY
  | "\\" ~ ("{" | "}" | "~" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
